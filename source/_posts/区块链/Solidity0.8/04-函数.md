---
title: 04-函数

date: 2022-06-21	

categories: Solidity0.8.17	

tags: [区块链,Solidity0.8.17]
---	

# 函数的定义

函数由关键字 `function` 声明，后面跟**函数名**、**参数**、**可视范围**、**状态可变性**、**返回值**的定义。函数可以定义在合约内部，也可以定义在合约外部。

```solidity
function fnName(<parameter list>)
    <visibility>
    <state mutability>
    [returns(<return type>)] {
    //语句
}

function fnName(<parameter types>)
    {internal|external}
    [pure|view|payable]
    [returns (<return types>)]{
        //...
}
```

- `function`: 声明函数的固定关键字
- `fnName` : 函数名，推荐小驼峰写法,更多参考
- `<parameter list>` : 参数列表(参数类型 + 参数名字)
- `<visibility>`: 可见性，public、external、internal、private
- `<state mutability>`: 状态可变性，pure、view、payable，不写 `pure/view/payable` 中任何一个，代表，函数既可以读取也可以写入状态变量。
- `returns (<return types>)`:返回值和返回参数类型

## 1.1 合约函数

注意：可以在合约内部或外部定义函数。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Fun {
	//合约内的函数
    function add(uint256 x, uint256 y) external pure returns (uint256) {
        return x + y;
    }
}

//合约之外的函数（也称为“自由函数”）始终具有隐式的 `internal` 可见性。 它们的代码包含在所有调用它们合约中，类似于内部库函数
function sum(uint256[] memory arr) pure returns (uint256 s) {
    for (uint256 i = 0; i < arr.length; i++) {
        s += arr[i];
    }
}
```

在合约之外定义的函数仍然在合约的上下文内执行。他们仍然可以访问变量 `this` ，也可以调用其他合约，将其发送以太币或销毁调用它们合约等其他事情。

与在合约中定义的函数的主要区别为：自由函数不能直接访问存储变量和不在他们的作用域范围内函数。

## 1.2 函数的输入参数

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Simple {
    uint256 sum;
	
	// 普通用法
    function add(uint256 a, uint256 b) public {
        sum = a + b;
    }
    
    function demo(uint256 _a) public pure returns (uint256) {
    	//函数参数可用在等号左边被赋值
        _a = 22;
        return _a;
    }
    
    //使用数组作为函数参数
    function demo(uint256[] memory _a) public returns (uint256[] memory) {
        a = _a;
        return _a;
    }
}
```

# 构造函数

构造函数关键字 `constructor`，Solidity 构造函数是一个特殊函数，它仅能在智能合约部署的时候调用一次，创建之后就不能再次被调用。

构造函数是可选的，只允许有一个构造函数，这意味着不支持重载。

**用处**: **Solidity 构造函数常用来进行状态变量的初始化工作。**

- 比如设置合约的 owner 权限
- 设置状态变量的初始值

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract ErrorModifier{
     address public owner;
    uint public count = 0;

     constructor(uint _x){
         owner = msg.sender;
         count = _x;
     }
}
```

**注意**：在合约创建的过程中，它的代码还是空的，所以直到构造函数执行结束，我们都不应该在其中调用合约自己的函数。(我们可以调用，但是不推荐调用)

**请注意**：不可以在构造函数中通过 this 来调用函数，因为此时真实的合约实例还没有被创建。

# mutability:状态可变性

- pure: 既**不读取也不修改状态变量**
  - 这种函数被称为**纯函数**
  
- view: 读取状态变量，但是不修改状态变量
  - 这种函数被称为**视图函数**
  - 状态变量的 Getter 方法默认是 view 函数。

- payable：用 payable 声明的函数可以接受发送给合约的以太币.
  - 如果未指定，该函数将自动拒绝所有发送给它的以太币

## 3.1 pure 不允许的操作

声明为 pure 函数，可以在函数声明里，添加 pure 关键字。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract C {
    function f(uint256 a, uint256 b) public pure returns (uint256) {
        return a * (b + 42);
    }
}
```

如果函数中存在以下语句，则被视为读取状态，编译器将抛出警告。

- **读取状态变量。**
  - **这也意味着读取 `immutable` 变量也不是一个 `pure` 操作。**
- **访问 `address(this).balance` 或 `<address>.balance`**
- **访问 `block`，`tx`， `msg` 中任意成员 （除 `msg.sig` 和 `msg.data` 之外）。**
- **调用任何未标记为 `pure` 的函数。**
- **使用包含特定操作码的内联汇编。**
  - **`TODO:` 这个不了解，需要用例子加深印象。**
- **使用操作码 `STATICCALL` , 这并不保证状态未被读取, 但至少不被修改。**

如果发生错误，`pure` 函数可以使用 `revert()`和 `require()` 函数来还原潜在的状态更改。还原状态更改不被视为 **状态修改**, 因为它只还原以前在没有`view` 或 `pure` 限制的代码中所做的状态更改, 并且代码可以选择捕获 revert 并不传递还原。这种行为也符合 STATICCALL 操作码。

**警告**:不可能在 EVM 级别阻止函数读取状态, 只能阻止它们写入状态 (即只能在 EVM 级别强制执行 `view` , 而 `pure` 不能强制)。

## 3.2 view 不允许的操作

可以将函数声明为 view 类型，这种情况下要保证不修改状态。声明为 view 图函数，可以在函数声明里，添加 view 关键字。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract C {
    function f(uint256 a, uint256 b) public view returns (uint256) {
        return a * (b + 42) + block.timestamp;
    }
}
```

**注解**: Getter 方法自动被标记为 view。

如果函数中存在以下语句，则被视为修改状态，编译器将抛出警告。

- **修改状态变量。**
- **触发事件。**
- **创建其它合约。**
- **使用 `selfdestruct`。**
- **通过调用发送以太币。**
- **调用任何没有标记为 view 或者 pure 的函数。**
- **使用底层调用**
  - **(TODO:这里是 call 操作么？)**
- **使用包含某些操作码的内联程序集。**

## 3.3 payable

### 一个加和减的 DEMO

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Counter{
    uint public count ;

    function add() external {
        count+=1;
    }

    function minus() external {
        count-=1;
    }
}
```

### 状态可变性的类型转换

如果满足下列条件，函数类型 A 可以隐式转换为函数类型:

- 它们的参数类型相同，返回类型相同，它们的内部/外部属性是相同的，并且 A 的状态可变性比 B 的状态可变性更具限制性

比如：

- pure 函数可以转换为 view 和 non-payable 函数
- view 函数可以转换为 non-payable 函数
- payable 函数可以转换为 non-payable 函数
- 其他的转换则不可以。

关于 `payable` 和 `non-payable` 的规则可能有点令人困惑，如果一个函数是 `payable` ，这意味着它也接受零以太的支付，因此它也是 `non-payable` 。 另一方面，`non-payable` 函数将拒绝发送给它的 以太币 Ether ， 所以 `non-payable` 函数不能转换为 `payable` 函数。

# 函数的签名/函数标识符

在变量的全局变量那一章，我们介绍了 `msg.data` `msg.sig`,分别是调用合约的完整的 calldata，以及**函数标识符**（calldata 的前四个字节）

### 查看 msg.data

代码如下

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Receiver {
    event Log(bytes data1, bytes4 data2);

    function transfer(address recipient, uint256 amount)
        external
        payable
        returns (address, uint256)
    {
        emit Log(msg.data, msg.sig);
        return (msg.sender, msg.value);
    }
}
```

- 输入:

  - `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`
  - `1`
  
- logs 结果:

  - data1

     （为了方便阅读，我拆分成如下）

    ```
    0xa9059cbb
    0000000000000000000000005b38da6a701c568545dcfcb03fcb875f56beddc4
    0000000000000000000000000000000000000000000000000000000000000001
    ```
    
  - data2
  
     结果如下

    - `0xa9059cbb`

- output 结果:

  - `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`
- `0`

### msg.data 中**函数标识符**的实现逻辑

核心: `bytes4(keccak256(bytes("transfer(address,uint256)")))`

一个函数调用数据的前 4 字节，指定了要调用的函数。这就是某个函数签名的 Keccak 哈希的前 4 字节（bytes32 类型是从左取值）。

函数签名被定义为基础原型的规范表达，而基础原型是**函数名称加上由括号括起来的参数类型列表，参数类型间由一个逗号分隔开，且没有空格。**.

代码如下，获取 Hash 后的值，和截取后的值

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract FunctionSelector {
    function getSelector(string calldata _func)
        external
        pure
        returns (bytes32, bytes4)
    {
        // _func 字符串通过 bytes 转为 bytes
        // 使用 keccak256 进行 Hash值运算
        // 使用 bytes4 截取 keccak256 返回的32位数据
        return (keccak256(bytes(_func)), bytes4(keccak256(bytes(_func))));
    }
}
```

**测试：**

1. 部署
2. 输入 `"transfer(address,uint256)"`
3. 获取结构
   1. `0xa9059cbb2ab09eb219583f4a59a5d0623ade346d962bcd4e46b11da047c9049b`
   2. `0xa9059cbb`

注意：以上仅仅是背后的原理展示，如果想要获取值，可以通过`.selector` 返回 ABI 函数选择器

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

library L {
    function f(uint256) external {}
}

contract C {
    function g() public pure returns (bytes4) {
        return L.f.selector;
    }
}
```

尽管可以对 public 或 external 的库函数进行外部调用，但此类调用会被视为 Solidity 的内部调用，与常规的 contract ABI 规则不同。外部库函数比外部合约函数支持更多的参数类型，例如递归结构和指向存储的指针。

因此，计算用于计算 4 字节选择器的函数签名遵循内部命名模式以及可对合约 ABI 中不支持的类型的参数使用内部编码。

以下标识符可以作为函数签名中的类型：

- 值类型, 非存储的（non-storage） `string` 及非存储的 `bytes`使用和合约 ABI 中同样的标识符。
- 非存储的数组类型遵循合约 ABI 中同样的规则，例如 `<type>[]`为动态数组以及 `<type>[M]` 为 `M` 个元素的动态数组。
- 非存储的结构体使用完整的命名引用，例如 `C.S` 用于`contract C { struct S { ... } }`.
- 存储的映射指针使用 `mapping(<keyType> => <valueType>) storage` 当`<keyType>` 和 `<valueType>` 是映射的键和值类型。
- 其他的存储的指针类型使用其对应的非存储类型的类型标识符，但在其后面附加一个空格及`storage` 。

# 函数的重载

Solidity 的函数重载，是指同一个作用域内，相同函数名可以定义多个函数。

**这些相同函数名的函数，参数(参数类型或参数数量)必须不一样。**，因为只有这样上一节介绍的函数签名中，才能签出来不同的函数选择器。

合约可以具有多个不同参数的同名函数，称为”重载”（overloading），这也适用于继承函数。

#### 选择重载函数 & 参数匹配

选择重载函数：通过将当前范围内的函数声明与函数调用中提供的参数相匹配，这样就可以选择重载函数。

如果所有参数都可以隐式地转换为预期类型，则该函数作为重载候选项。如果一个匹配的都没有，解析失败。

⚠️：返回参数不作为重载解析的依据。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract A {
    function f(uint8 val) public pure returns (uint8 out) {
        out = val;
    }

    function f(uint256 val) public pure returns (uint256 out) {
        out = val;
    }
}

contract B {
    A a;

    // Member "f" not unique after argument-dependent lookup in contract A.
    // function test1() public view returns (uint256) {
    //     uint256 tar = a.f(8);
    //     return tar;
    // }

    function test2() public view returns (uint256) {
        uint256 tar = a.f(256);
        return tar;
    }
}
```

在 Remix 里,部署 A 合约，会将两个方法都渲染出来，调用 `f(50)`/`f(256)` 都可以。

但是实际调用里，在其他合约内调用 `f(50)` 会导致类型错误，因为 `50` 既可以被隐式转换为 `uint8`也可以被隐式转换为 `uint256`。 另一方面，调用 `f(256)` 则会解析为`f(uint256)` 重载，因为 `256` 不能隐式转换为 `uint8`。

# modifier:函数修改器

Solidity 中关键字 `modifier` 用于声明一个函数修改器。

- **意义**:我们可以将一些通用的操作提取出来，包装为函数修改器，来提高代码的复用性，改善编码效率。是函数高内聚，低耦合的延伸。

- 作用: 常用于在函数执行前检查某种前置条件。

  - 比如地址对不对，余额是否充足，参数值是否允许等
  - 修改器内可以写逻辑
  
- 特点:是一种合约属性，可被继承，同时还可被派生的合约重写(override)。（修改器 modifier 是合约的可继承属性，并可能被派生合约覆盖 , 但前提是它们被标记为 virtual）。

  - `_` 符号可以在修改器中出现多次，每处都会替换为函数体。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract ErrorModifier{
     address public owner;
    uint public count = 0;
    
     constructor(){
         owner = msg.sender;
     }
     
     function add() external{
     	//普通判断,非常的啰嗦
         require(msg.sender==owner,"must owner address");
         count++;
     }
     
     // 下面就是函数修改器
     modifier onlyOwner(){
         require(msg.sender==owner,"must owner address");
         _;
     }
     function add() external onlyOwner{
         count++;
     }
     
     // 函数修改器:带参数
      modifier greaterThan(uint _x){
         require(_x > 10,"must be greater than 10");
         _;
     }

     function fnA(uint _x) external onlyOwner greaterThan( _x){
         count=_x;
     }
     
     //防重载的函数修改器，这种使用方法，在低版本的 solidity 中可以防止重入攻击。
      modifier noReentrant() {
        require(!locked, "no reentrant");
        locked = true;
        _;
        locked = false;
    }

}
```

如果你想访问定义在合约 C 的 修改器 modifier m ， 可以使用 C.m 去引用它，而不需要使用虚拟表查找。

只能使用在当前合约或在基类合约中定义的 修改器 modifier , 修改器 modifier 也可以定义在库里面，但是他们被限定在库函数使用。

如果同一个函数有多个 修改器 modifier，它们之间以空格隔开，修改器 modifier 会依次检查执行。

修改器不能隐式地访问或改变它们所修饰的函数的参数和返回值。 这些值只能在调用时明确地以参数传递。

修改器 modifier 或函数体中显式的 return 语句仅仅跳出当前的 修改器 modifier 和函数体。 返回变量会被赋值，但整个执行逻辑会从前一个 修改器 modifier 中的定义的 `_` 之后继续执行。

警告:在早期的 Solidity 版本中，有 修改器 modifier 的函数， return 语句的行为表现不同。用 `return`; 从修改器中显式返回并不影响函数返回值。 然而，修改器可以选择完全不执行函数体，在这种情况下，返回的变量被设置为默认值，就像该函数是空函数体一样。

`_` 符号可以在修改器中出现多次，每处都会替换为函数体。

修改器 modifier 的参数可以是任意表达式，在此上下文中，所有在函数中可见的符号，在 修改器 modifier 中均可见。 在 修改器 modifier 中引入的符号在函数中不可见（可能被重载改变）。

# 全局：数学和密码学函数

在全局命名空间中已经预设了一些特殊的变量和函数，他们主要用来提供关于区块链的信息或一些通用的工具函数。后续会详细介绍，这里简单的介绍几个全局函数

## 7.1 数学和密码学函数

Solidity 也提供了内置的数学和密码学函数：

数学函数：

- ```
  addmod(uint x, uint y, uint k) returns (uint)
  ```

  - 计算 `(x + y) % k`，加法会在任意精度下执行，并且加法的结果即使超过 `2**256` 也不会被截取。从 0.5.0 版本的编译器开始会加入对 `k != 0` 的校验（assert）。

- ```
  mulmod(uint x, uint y, uint k) returns (uint)
  ```

  - 计算 `(x * y) % k`，乘法会在任意精度下执行，并且乘法的结果即使超过 `2**256` 也不会被截取。从 0.5.0 版本的编译器开始会加入对 `k != 0` 的校验（assert）。

密码学函数：

- ```
  keccak256((bytes memory) returns (bytes32)
  ```

  - 计算 Keccak-256 哈希，之前 keccak256 的别名函数 **sha3** 在 **0.5.0** 中已经移除。。

- ```
  sha256(bytes memory) returns (bytes32)
  ```

  - 计算参数的 SHA-256 哈希。

- ```
  ripemd160(bytes memory) returns (bytes20)
  ```

  - 计算参数的 RIPEMD-160 哈希。

- ```
  ecrecover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) returns (address)
  ```

  - 利用椭圆曲线签名恢复与公钥相关的地址，错误返回零值。
  - 函数参数对应于 ECDSA 签名的值:
    - r = 签名的前 32 字节
    - s = 签名的第 2 个 32 字节
    - v = 签名的最后一个字节
  - ecrecover 返回一个 address, 而不是 address payable。
  - `ecrecover` 的[使用案例](https://ethereum.stackexchange.com/questions/1777/workflow-on-signing-a-string-with-private-key-followed-by-signature-verificatio)

## 7.2 密码学函数

### 密码学 keccak256 和 encodePacked/encode

```
keccak256((bytes memory) returns (bytes32)
```

**keccak256**: 返回结果是 bytes32

这些编码函数可以用来构造函数调用数据，而不用实际进行调用。此外，`keccak256(abi.encodePacked(a, b))` 是一种计算结构化数据的哈希值（尽管我们也应该关注到：使用不同的函数参数类型也有可能会引起“哈希冲突” ）的方式，不推荐使用的 `keccak256(a, b)` 。

如果是多个参数，推荐使用 `encode`；`encodePacked` 因为哈希碰撞，容易导致参数不同，结果相同

> [AAA,BB] 和 [AA,ABB] 通过 encodePacked 得到的结果相同

### 密码学: ecrecover

```
ecrecover(bytes32 hash, uint8 v, bytes32 r, bytes32 s) returns (address)
```

利用椭圆曲线签名恢复与公钥相关的地址，错误返回零值。

函数参数对应于 ECDSA 签名的值:

- r = 签名的前 32 字节
- s = 签名的第 2 个 32 字节
- v = 签名的最后一个字节

`ecrecover` 返回一个 address, 而不是 `address payable` 。他们之前的转换参考 `address payable` ，如果需要转移资金到恢复的地址。[参考案例](https://ethereum.stackexchange.com/questions/1777/workflow-on-signing-a-string-with-private-key-followed-by-signature-verificatio)

#### 注意

如果你使用 `ecrecover` ，需要了解，在不需要知道相应的私钥下，签名也可以转换为另一个有效签名（可能是另外一个数据的签名）。在 Homestead 硬分叉，这个问题对于 *transaction* 签名已经解决了(查阅 EIP-2)。 不过 `ecrecover` 没有更改。

除非需要签名是唯一的，否则这通常不是问题，或者是用它们来识别物品。 OpenZeppelin 有一个 [ECDSA](https://docs.openzeppelin.com/contracts/2.x/api/cryptography#ECDSA) 助手库 ，可以将其用作 `ecrecover` 的”包装“，而不会出现此问题。

在一个私链上，你很有可能碰到由于 sha256、ripemd160 或者 ecrecover 引起的 **Out-of-Gas**。这个原因就是他们被当做所谓的预编译合约而执行，并且在第一次收到消息后这些合约才真正存在（尽管合约代码是硬代码）。发送到不存在的合约的消息非常昂贵，所以实际的执行会导致 Out-of-Gas 错误。在你的合约中实际使用它们之前，给每个合约发送一点儿以太币，比如 1 Wei。这在官方网络或测试网络上不是问题。

**ecrecover**:这个比较复杂，请在下面的 **通过智能合约验证签名** 例子详细查看；

### 案例 1:通过智能合约验证签名

做一个 DEMO：链上对任意消息进行加密，加密消息在链下使用私钥再次加密，然后对再次加密的信息进行校验。

1. 获取消息的 Hash 值

   - `hash = msgHash(_message);`

2. 在【链下】将 hash 使用 MetaMask 进行私钥签名;

   - `_signature = metaMaskSignHash(hash,addressPrivateKey)`
   - 这里相当于在 Metamask 对 hash 做第二次的 **keccak256 Hash** 转换，转换时添加了`"\x19Ethereum Signed Message:\n32"`

3. 使用ecrecover方法恢复签名地址

   - `ecrecoverAddress = recoverAds(hash,_signature)`
   - 这里可以恢复 MetaMask 签名时候使用的地址
   
4. 校验签名结果是否正确
   ecrecoverAddress == addressPublicKey ? “验证成功” : “验证失败”;

#### 代码如下

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract VerifySig {
    /**
    功能:校验签名结果是否正确
    注意 _signature 是 bytes 类型的
     */
    function verify(
        address addressPublicKey,
        string calldata _message,
        bytes calldata _signature
    ) external pure returns (bool) {
        bytes32 hash = msgHash(_message);
        // bytes32 _signature = metaMaskSignHash(hash,addressPrivateKey); // 这是在链下操作
        address ecrecoverAddress = recoverAds(hash, _signature);
        return ecrecoverAddress == addressPublicKey;
    }

    function msgHash(string calldata _message)
        public
        pure
        returns (bytes32 keccakFirst)
    {
        keccakFirst = keccak256(abi.encodePacked(_message));
    }

    function msgHash2(bytes32 _msgHash)
        internal
        pure
        returns (bytes32 keccakSecond)
    {
        // 两次2次签名，据说是数学层面上1次签名有被破解的可能。[我没有亲自验证过]
        keccakSecond = keccak256(
            abi.encodePacked("\x19Ethereum Signed Message:\n32", _msgHash)
        );
    }

    function recoverAds(bytes32 _msgHash, bytes calldata _signature)
        public
        pure
        returns (address)
    {
        // metamsk 签名会在原有消息上添加  "\x19Ethereum Signed Message:\n32"，所以需要处理一下
        bytes32 metamaskInputHash = msgHash2(_msgHash);
        // r为点的x坐标，s为点的y坐标，v是坐标的奇偶检验标识符
        // v是用于说明那个点才是真正符合结果的点
        // https://www.cnblogs.com/wanghui-garcia/p/9662140.html
        // https://www.jianshu.com/p/090f605f1842/
        (bytes32 r, bytes32 s, uint8 v) = _split(_signature);

        address ecrecoverAddress = ecrecover(metamaskInputHash, v, r, s);
        return ecrecoverAddress;
    }

    function _split(bytes memory _signature)
        internal
        pure
        returns (
            bytes32 r,
            bytes32 s,
            uint8 v
        )
    {
        // 需要内联汇编进行分割，合约没有别的方法
        require(_signature.length == 65, "invalid signature length");
        assembly {
            r := mload(add(_signature, 32))
            s := mload(add(_signature, 64))
            // v := mload(add(_signature, 96))
            // 因为 v 不是 bytes32，是 uint8数字，uint8数字只占1位，所以使用 byte(0)转换
            v := byte(0, mload(add(_signature, 96)))
        }
    }
}
```

#### 链下签名和 Remix 验证

使用浏览器控制台进行签名，需要安装 MateMask

```solidity
// 1.打开 ethereum
ethereum.enable()

// 2.赋值地址。这里的地址是 MateMask 的默认地址
const address = "0xffD0d80c48F6C3C5387b7cfA7AA03970bdB926ac";

// 3.赋值Hash。 使用 msgHash1 方法，输入 "ABC" 获取到的结果
const hash = "0xe1629b9dda060bb30c7908346f6af189c16773fa148d3366701fbaa35d54f3c8"

// 4. 呼起 MataMask 签名
ethereum.request({method:"personal_sign",params:[address,hash]});

// 5. 打开返回的 Promise {<pending>}，拷贝 PromiseResult 值
0x66029be70a055a4abc293072c76550ffaecb2adb9fc3be2366d78bc498e008d06b6ddbfef97392a27a58737c33b059e09bb069261bdc41f9f0d8d1bc6e0b7ae31c

// 6. 在 recoverAds 中验证恢复的地址是否为签名地址。
上面的 PromiseResult 值是 _signature
上面的 hash 值是 _msgHash

// 7. 在 verify 中再次校验
```

扩展阅读: 在线进行签名的网站: https://metamask.github.io/test-dapp/

## 7.3 全局：ABI 编码及解码函数

ABI 全名 Application Binary Interface。ABI 用于底层调用的辅助使用；在合约调用合约的时候使用，可以不知道对方的合约源码，只需要知道链上逻辑即可。

- ABI 编码
  - `abi.encode(...) returns (bytes)`： :ref:`ABI <ABI>` - 对给定参数进行编码
  - `abi.encodePacked(...) returns (bytes)`：对给定参数执行 :ref:`紧打包编码 <abi_packed_mode>` ，注意，可以不明确打包编码。
  - `abi.encodeWithSelector(bytes4 selector, ...) returns (bytes)`： :ref:`ABI <ABI>` - 对给定第二个开始的参数进行编码，并以给定的函数选择器作为起始的 4 字节数据一起返回
  - `abi.encodeWithSignature(string signature, ...) returns (bytes)`：等价于 `abi.encodeWithSelector(bytes4(keccak256(signature), ...)`
  - `abi.encodeCall(function functionPointer, (...)) returns (bytes memory)`: 使用 tuple 类型参数 ABI 编码调用 `functionPointer` 。执行完整的类型检查, 确保类型匹配函数签名。结果和 `abi.encodeWithSelector(functionPointer.selector, (...))` 一致。
- ABI 解码
  - `abi.decode(bytes memory encodedData, (...)) returns (...)`: 对给定的数据进行 ABI 解码，而数据的类型在括号中第二个参数给出 。 例如: `(uint a, uint[2] memory b, bytes memory c) = abi.decode(data, (uint, uint[2], bytes))`

### 7.3.1 encode: 会补零

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract AbiDecode {
    function encode(string memory a, string memory b)
        external
        pure
        returns (bytes memory)
    {
        return abi.encode(a, b);
    }
}
/**
输入如下参数和返回结果
1.AA,BB
    0x
    0000000000000000000000000000000000000000000000000000000000000040
    0000000000000000000000000000000000000000000000000000000000000080
    0000000000000000000000000000000000000000000000000000000000000002
    4141000000000000000000000000000000000000000000000000000000000000
    0000000000000000000000000000000000000000000000000000000000000002
    4242000000000000000000000000000000000000000000000000000000000000
2.AAA,BB
    0x
    0000000000000000000000000000000000000000000000000000000000000040
    0000000000000000000000000000000000000000000000000000000000000080
    0000000000000000000000000000000000000000000000000000000000000003
    4141410000000000000000000000000000000000000000000000000000000000
    0000000000000000000000000000000000000000000000000000000000000002
    4242000000000000000000000000000000000000000000000000000000000000
3.AA,ABB
    0x
    0000000000000000000000000000000000000000000000000000000000000040
    0000000000000000000000000000000000000000000000000000000000000080
    0000000000000000000000000000000000000000000000000000000000000002
    4141000000000000000000000000000000000000000000000000000000000000
    0000000000000000000000000000000000000000000000000000000000000003
    4142420000000000000000000000000000000000000000000000000000000000
*/
```

### 7.3.2 encodePacked不会补零

不补零，容易导致碰撞错误。（两个参数拼在一起，导致参数不同，结果相同）

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract AbiDecode {
    function encodePacked(string memory a, string memory b)
        external
        pure
        returns (bytes memory)
    {
        return abi.encodePacked(a, b);
    }
}
/**
输入如下参数和返回结果
1.AA,BB
    0x41414242
2.AAA,BB
    0x4141414242
3.AA,ABB
    0x4141414242
[AAA,BB] 和 [AA,ABB] 得到的结果相同
*/
```

#### 解决 `encodePacked` 的哈希碰撞问题

可以在要编码的数据中间加一个固定的值，如果

**代码如下:**

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract AbiDecode {
    function encodePacked(string calldata _test1, string calldata _test2)
        public
        pure
        returns (bytes memory)
    {
        uint256 x = 123;
        return abi.encodePacked(_test1, x, _test2);
    }
}
```

**结果如下:**

```solidity
/**
输入如下参数和返回结果
1.AA,BB
    0x4141000000000000000000000000000000000000000000000000000000000000007b4242
2.AAA,BB
    0x414141000000000000000000000000000000000000000000000000000000000000007b4242
3.AA,ABB
    0x4141000000000000000000000000000000000000000000000000000000000000007b414242
[AAA,BB] 和 [AA,ABB] 因为间隔了数据，所以得到的结果不相同
*/
```

#### 注意

这些编码函数可以用来构造函数调用数据，而不用实际进行调用。此外，`keccak256(abi.encodePacked(a, b))` 是一种计算结构化数据的哈希值（尽管我们也应该关注到：使用不同的函数参数类型也有可能会引起“哈希冲突” ）的方式，不推荐使用的 `keccak256(a, b)` 。

### 7.3.3 decode

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract AbiDecode {
    struct MyStruct {
        string name;
        uint256[2] nums;
    }

    function encode(
        uint256 x,
        address addr,
        uint256[] calldata arr,
        MyStruct calldata myStruct
    ) external pure returns (bytes memory) {
        return abi.encode(x, addr, arr, myStruct);
    }

    function decode(bytes calldata data)
        external
        pure
        returns (
            uint256 x,
            address addr,
            uint256[] memory arr,
            MyStruct memory myStruct
        )
    {
        (x, addr, arr, myStruct) = abi.decode(
            data,
            (uint256, address, uint256[], MyStruct)
        );
    }
}
```

**合约测试**

- 部署

- ```
  encode
  ```

  - 参数如下:
  - `1`
  - `0xffD0d80c48F6C3C5387b7cfA7AA03970bdB926ac`
  - `[1,2,3]`
  - `["Anbang",[2,3]]`

- 得到的结果，进行 `decode`

### 7.3.4 abi.encodeWithSelector

这是获取函数签名使用的，第一个参数为函数选择，如下是第二章在介绍地址类型的时候，staticcall 静态调用 用法的参数，需要由 `abi.encodeWithSelector` 计算出来。函数的参数按照顺序写在函数名之后即可。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

// 被调用的合约
contract Hello {
    function echo() external pure returns (string memory) {
        return "Hello World!";
    }
}

// 调用者合约
contract SoldityTest {
    function callHello(address _ads) external view returns (string memory) {
        // 编码被调用者的方法签名
        bytes4 methodId = bytes4(keccak256("echo()"));

        // 调用合约
        (bool success, bytes memory data) = _ads.staticcall(
            abi.encodeWithSelector(methodId)
        );
        if (success) {
            return abi.decode(data, (string));
        } else {
            return "error";
        }
    }
}
```

### 7.3.5 abi.encodeWithSignature

这是获取函数签名使用的，第一个参数为函数的名字和参数类型，如下是第二章在介绍地址类型的时候，call 用法的参数，需要由 `abi.encodeWithSignature` 计算出来。函数的参数按照顺序写在函数名之后即可。

```solidity
function call_Test1_setNameAndAge(
    address _ads,
    string memory _name,
    uint256 _age
) external payable {
    bytes memory data = abi.encodeWithSignature(
        "setNameAndAge(string,uint256)",
        _name,
        _age
    );
    (bool success, bytes memory _bys) = _ads.call{value: msg.value}(data);
    require(success, "Call Failed");
    bys = _bys;
}
```

