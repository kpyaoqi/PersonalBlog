---
title: 03-变量

date: 2022-06-20	

categories: Solidity0.8.17	

tags: [区块链,Solidity0.8.17]
---	

# 变量基础知识

回顾一下前面两章提到的变量：

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract DataTypes {
    string public myString = "hello world";
    bool public b = true;
    uint256 public u = 123;

    int256 public i = -123;
    int256 public minInt = type(int256).min; // 获取最小值
    int256 public maxInt = type(int256).max; // 获取最大值
    address public ads = 0xffD0d80c48F6C3C5387b7cfA7AA03970bdB926ac;
    bytes32 public bys32 = "abc";
}
```

## 1.1 初始默认值

**Solidity 是一种静态类型语言**，这意味着需要在声明期间指定变量类型。

在 Solidity 中没有 null 或者 undefined 的概念，但是新声明的变量总是有一个默认值，具体的默认值跟类型相关，比如 int 类型的默认值为 0。每个变量声明时，都有一个基于其类型的默认值。

#### 默认值总结

- string: `""`
- bool: `false`
- int256: `0`
- uint256: `0`
- address: `0x0000000000000000000000000000000000000000`
- bytes32: `0x0000000000000000000000000000000000000000000000000000000000000000`
- enum: `0`
- 动态数组: `[]`
- 定长数组: 每个元素的默认值
- `mapping` / `strucr` 均为所在类型的默认值

## 1.2 作用域和声明

Solidity 中的作用域规则遵循了 C99：

### 1.2.1 作用域的规则

- 变量将会从它们被声明之后可见，直到一对 `{}` 块的结束。
- 对于参数形式的变量（例如：函数参数、修饰器参数、catch 参数等等）在其后接着的代码块内有效。
  - 这些代码块是函数的实现，catch 语句块等。
  - 有一个例外，在 for 循环语句中初始化的变量，其可见性仅维持到 `for` 循环的结束。
- 那些定义在代码块之外的变量，比如函数、合约、自定义类型等等，并不会影响它们的作用域特性。
  - 意味着你可以在实际声明状态变量的语句之前就使用它们，并且递归地调用函数。

在 Solidity 中，如果在内部作用域中使用和外层相同的变量名，会收到警告信息。这种警告是告诉开发者`外层声明的变量被“覆盖”`了，谨慎检查下是不是期望的。

上面例子中的 f 函数的 return 值 可以改写为下面的：

```solidity
function f() public pure returns (uint256 x) {
    x = 1;
    {
        x = 2;
        uint256 x;
    }
}
```

### 1.2.2 必须先声明再赋值

在 Solidity 现在的版本中，变量必须先声明再赋值，顺序不能倒。

0.5.0 版本之前，一个变量声明在函数的任意位置，都可以使他在整个函数范围内可见。从 0.5.0 版本开始以后就不能这样了。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract C {
    // ✅
    function t() public pure returns (uint256) {
        uint256 x;
        x = 2;
        return x;
    }

    // ❌
    function f() public pure returns (uint256) {
        // DeclarationError: Undeclared identifier.
        // "x" is not (or not yet) visible at this point.
        x = 2;
        uint256 x;
        return x;
    }
}
```

### 1.2.3 合约外定义的类型和函数

- 合约外面可以定义函数和数据结构
  - 定义在合约外面的函数，叫自由函数
  - 定义在合约外面的类型，可以被多个合约使用
- 不可以定义变量
  - 但是可以定义常量，常量那一节有介绍

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

// 结构:
//      定义在合约外面
//      可以被多个合约同时使用
struct Book {
    string title;
    string author;
    uint256 book_id;
}

// 自由函数:
//      定义在合约外面
//      没有可见性
function getBalance() view returns (uint256) {
    return address(msg.sender).balance;
}


function add(uint256 a_, uint256 b_) pure returns (uint256) {
    return a_+b_;
}

contract Demo1 {
    Book public book1 = Book("Solidity", "Anbang", 1);
    function f() public view returns (uint256) {
        return getBalance();
    }
    function test() public pure returns (uint256) {
        return add(100,200);
    }

}

contract Demo2 {
    Book public book2 = Book("Solidity 2", "Anbang", 2);
}
```

## 1.3 变量的三种状态

按作用域划分，状态可以分为下面三种状态：

- 状态变量:
  - 变量值永久保存在智能合约存储空间中，相当于属于已经写入到区块链中，可以随时调用，除非该条链消失。
  - 特点:定义在智能合约的存储空间中
- 局部变量:
  - 变量值仅在函数执行过程中有效,供函数内部使用；调用函数时，在虚拟机的内存中；函数退出后，变量无效。类似"闭包"的特性。
  - 特点: 定义在函数内部
- 全局变量:
  - 保存在全局命名空间，用于获取区块链相关信息的特殊变量。
  - 特点:存在于 EVM 虚拟机中，不用定义，直接获取即可。

### 1.3.1 状态变量

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Var{
    uint256 public myUint = 123;
    function changeMyUint (uint256 x) external returns (uint256 ){
        myUint = x;
        return myUint;
    }
}
```

### 1.3.2 局部变量

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Var{
     function test() external pure returns (uint256){
        uint256 local = 2;// 局部变量
        return local;
    }
}
```

### 1.3.3 全局变量

- `msg.sender`
- `msg.value`
- `block.timestamp`
- `block.number`

先简单了解下，后面会有一节进行详细介绍。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;
contract Demo1 {
    address public owner = msg.sender; // 在状态变量中使用

    // 在函数内使用
    function global() external view returns(address,uint256,uint256){
        return(msg.sender,block.timestamp,block.number);
    }
}
```

# Constant 常量

- 普通变量与常量:普通的状态变量，添加 `constant` 关键词即可声明为常量
- 与常规状态变量相比，**常量的 gas 要低很多**。
- 常量名字一般使用**全大写**。
- 常量**赋值后不可以修改**。
- 常量必须**声明和初始化一起完成**，否则编译不通过。
- 常量的值储存原理
  - 常量的值在编译器确定，因为在编译器确定，所以**不能定义在函数内**。
  - 编译器并不会为 `constant` 常量在 `storage` 上预留空间，它们的每次出现都会被替换为相应的常量表达式（它可能被优化器计算为实际的某个值）。
  - 因为不是储存在`storage` 上，所以函数内读取常量不算`view`，可以使用 pure
  - 因为不是储存在`storage` 上，所以可以在任意位置定义常量，比如在合约外面
  - 也可以在文件级别定义 constant 变量（0.7.2 之后的特性）。
- 引用类型只支持字符串
  - 不是所有的类型都支持常量，当前支持的仅有**值类型（包括地址类型）/字符串/bytes**。
- 可以使用内建函数赋值常量

## 2.1 常见的赋值方式

如果状态变量声明为 `constant`(常量)。在这种情况下，只能使用那些在编译时有确定值的表达式来给它们赋值。

- 允许可能对内存分配产生 side effect（副作用）的表达式，但那些可能对其他内存对象产生副作用的表达式则不允许。
  - 内建（built-in）函数 `keccak256` ， `sha256` ， `ripemd160` ，`ecrecover` ， `addmod` 和 `mulmod`是允许的（即使他们确实会调用外部合约， `keccak256` 除外）。
  - 允许内存分配器的副作用的原因是它可以构造复杂的对象，例如：查找表（lookup-table）。 此功能尚不完全可用。

### 2.1.1 运算符赋值

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Demo1 {
    uint256 public constant VERSION = 1+1;

    // 100个ETH
    // 100000000000000000000
    uint256 public  constant VALUE = 100 * 10**18; // 运算符赋值

    // 1小时
    uint256 public  constant H = 60 * 60;
}
```

### 2.1.2 密码学函数赋值

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract C {
    uint256 public constant a = addmod(4, 5, 3);
    uint256 public constant b = mulmod(4, 5, 3);
    bytes32 public constant c1 = sha256("Hello");
    bytes32 public constant c2 = ripemd160("Hello");
    bytes32 public constant myBytes32 = keccak256("Hello");
}
```

### 2.1.3  禁止的一些赋值

不允许使用状态变量/区块链数据来赋值，也不允许外部合约调用来赋值。

任何通过访问 storage，区块链数据（例如 `block.timestamp`,`address(this).balance` 或者 `block.number`）或执行数据（ `msg.value` 或`gasleft()` ） 或对外部合约的调用来给它们赋值都是不允许的。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Demo {
    address public ads1 = msg.sender;
    // address public constant ads2 = msg.sender;
}
```

# Immutable 不可变量

上一节我们学习了常量，常量的值是在编译器确定，因为在编译器确定，常量必须声明和初始化一起做掉，否则编译不通过。这就给很多只需要一次赋值，但是又必须需要动态赋值的场景带来了不变。比如合约的 owner 地址等场景。

然后上面的苦恼，可以通过不可变量来解决。如果我们想要一个变量，赋值后就不可以修改，而且值是部署时候动态赋值，那么可以使用不可变量的类型。通过 `immutable` 关键字可以声明为不可变量，不可变量的限制要比声明为常量(`constant`) 的变量的限制少：

不可变量**可以声明和赋值一起做掉，也可以 storage 中声明**，在合约的构造函数中赋值。无论在哪里赋值，只能赋值一次，也带来更多的安全性。

- 原理: 在部署的时候确定变量的值，它是一个运行时赋值。

- 特点:它既有 constant 常量不可修改和 Gas 费用低的优势，又有变量动态赋值的优势。

- 原则:

  - `immutable` 可以声明和初始化一起做掉，也可以部署时在`constructor`中做掉。
  - `immutable` 必须在`constructor`运行截止时就赋值
  - `immutable`不能用在引用数据类型上

## 3.1 部署后不可以修改

注意点：不可变量只能赋值一次，以后就不能再次改变了

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract A {
    address public immutable adsImmut;
    address public immutable ads = address(0);

    constructor() {
        adsImmut = msg.sender;

        // Immutable state variable already initialized.
        // ads = msg.sender;
    }

    // immutable 部署后不可以修改，如果尝试修改immutable类型变量，会报错
    // Cannot write to immutable here: Immutable variables can only
    // be initialized inline or assigned directly in the constructor.
    // function changeImmutable() external {
    //     adsImmut = msg.sender;
    // }
}
```

## 3.2 不可以在赋值前读取

并且在赋值之后才可以读取 immutable 变量，如果赋值之前读取会报错。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract A {
    address public immutable adsImmut;
    address public ads;

    constructor() {
        // Immutable variables cannot be read before they are initialized.
        // ads = adsImmut;

        adsImmut = msg.sender;
        ads = adsImmut;
    }
}
```

## 3.3 不可以在 `constructor` 之外赋值其他不可变量

不可变量可以在声明时赋值，不过只有在合约的构造函数执行时才被视为视为初始化。这意味着，你不能用一个依赖于不可变量的值初始化另一个不可变量。

因为不可变量在构造函数中才真正赋值，所以在合约的构造函数中这样做。这样的机制是为了防止对状态变量初始化和构造函数顺序的不同解释，特别是继承时，出现问题。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Demo {
    uint256 public a1 = 1;
    uint256 public a2 = a1;

    uint256 public immutable b1 = 1;
    // Immutable variables cannot be read before they are initialized.
    // uint256 public b2 = b1;
    uint256 public b2;

    uint256 public immutable b3;

    constructor() {
        b2 = b1;
        b3 = b1;
    }
}
```

核心：在部署的时候确定变量的值，它是一个运行时赋值。

## 3.4constant 和 immutable 总结

####  值的确定时机不同

状态变量声明为 constant (常量)或者 immutable （不可变量），在这两种情况下，合约一旦部署之后，变量将不在修改。

- 对于 constant 常量, 他的值在编译器确定.
- 对于 immutable, 它的值在部署时确定。

####  gas 不同

与常规状态变量相比，常量和不可变量的 gas 成本要低得多。

- 对于常量，赋值给它的表达式将复制到所有访问该常量的位置，并且每次都会对其进行重新求值。这样可以进行本地优化。
- 不可变变量在构造时进行一次求值，并将其值复制到代码中访问它们的所有位置。 对于这些值，将保留 32 个字节，即使它们适合较少的字节也是如此。很多时候常量的 gas 更低。

如果可以使用常量的时候，推荐使用常量。

####  支持的数据不同

不是所有类型的状态变量都支持用 `constant` 或 `immutable` 来修饰

- 当前`constant`仅支持值类型和引用类型中的 string 和 bytes
- `immutable`仅支持值类型

# 变量名的命名规则

在为变量命名时，请记住以下规则：

- 禁止使用**保留关键字**作为变量名。
- 变量名首字母禁止使用数字，必须以字母或下划线开头。
- 变量名大小写敏感。

### 1.禁止使用**保留关键字**作为变量名

禁止使用**保留关键字**作为变量名。例如：`break` / `boolean` / `contract` 这些是无效的变量名。

- abstract
- after
- alias
- apply
- auto
- case
- catch
- copyof
- default
- define
- final
- immutable
- implements
- in
- inline
- let
- macro
- match
- mutable
- null
- of
- override
- partial
- promise
- reference
- relocatable
- sealed
- sizeof
- static
- supports
- switch
- try
- typedef
- typeof
- unchecked

### 2.变量名必须以字母或者下划线开头

禁止使用数字作为变量名的开始，例如：`123abc` 是一个无效的变量名，但是 `_123abc` 是一个有效的变量名。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Demo {
    uint8 public abc = 1;
    uint8 public _123abc = 1;

    // Expected identifier but got 'ILLEGAL'
    uint8 public 123abc = 1;
}
```

### 3.变量名大小写敏感

例如：Name 和 name 是两个不同的变量。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

contract Demo {
    string public name = "Anbang";
    string public constant Name = "Anbang";
}
```

# 变量的可见性

可见性仅存在于状态变量和函数中

- 局部变量的可见性仅限于定义它们的函数，函数有四可见型，分别是 `private` / `external` / `internal` / `public`
- 状态变量可以有三种可见型，分别是 `private` / `internal` / `public`

我们这里重点介绍状态变量可见型。

internal 和 private 类型的变量不能被外部访问，而 public 变量能够被外部访问。

警告: 设置为 `private`或 `internal`，只能防止其他合约读取或修改信息，但它仍然可以在链外查看到。你不要想着通过设置可见型，让别人看不到你的代码。反编译可以得到大部分需要的逻辑。

## 5.1 private

**private**: 私有，仅在当前合约中可以访问

在继承的合约内不可访问，私有状态变量就像内部变量一样，但它们在派生合约中是不可见的。

## 5.2 internal(默认)

**internal**: 内部可视(合约内部和被继承的子合约中都可视)

- 状态变量如果不显示声明，**默认是 internal 权限**
- 内部可见性状态变量只能在它们所定义的合约和派生合同中访问。 它们不能被外部访问。 这是状态变量的默认可见性。

## 5.3 external

**external**: 外部可视(合约外部可视，在内部是不可见)

**`external` 不能声明在状态变量上，只能标识在函数上**,因为如果一个状态变量在合约自身如果没有办法读取的话，那就没有存在的必要了。

## 5.4 public

**public**: 公开可视(合约内部，被继承的，外部都可以调用)

# 全局：时间单位

在做时间相关业务时候可以使用 分钟，小时，天，周的概念。

## 6.1 基本用法

秒是缺省时间单位,可以不写，在时间单位之间，数字后面带有 `seconds`、 `minutes`、 `hours`、 `days`、 `weeks` 可以进行换算，基本换算关系如下：

- `1 == 1 seconds`
- `1 minutes == 60 seconds`
- `1 hours == 60 minutes`
- `1 days == 24 hours`
- `1 weeks == 7 days`

## 6.2 两种时间逻辑

注意：由于闰年造成的每年不都是 365 天、每天不都是 24 小时，所以**如果你要使用这些单位计算日期和时间**，请注意这个问题。因为是无法预测的，所以需要借助外部的预言机来对一个确定的日期代码库进行时间矫正。

时间在项目中有两种逻辑

1. 像之前众筹合约里介绍的那样，使用持续时间来代表时间。比如持续 2 两小时结束，常见于众筹/拍卖合约
2. 到某个时间点开始抢购活动，比如到 XX 年 XX 月 XX 日 XX 分 XX 秒，开启抢购，这种需要借助预言机才能正确完成

请按照自己的业务需求选择合适的时间方式。

# 全局：区块和交易属性

分别是 `block` / `msg` / `tx` 三个全局变量，因为功能相似，我们把 `blockhash()` 和 `gasleft()` 这两个全局函数也一起介绍。

## 预览

| 名称 (返回值)                                    | 返回                                                         |
| ------------------------------------------------ | ------------------------------------------------------------ |
| block.basefee (uint256)                          | 当前区块的基本费用（ [EIP-3198](https://eips.ethereum.org/EIPS/eip-3198) 和 [EIP-1559](https://eips.ethereum.org/EIPS/eip-1559)） |
| block.chainid (uint256)                          | 当前链 id                                                    |
| block.difficulty (uint256)                       | 当前区块的难度                                               |
| block.gaslimit (uint256)                         | 当前区块的 gaslimit                                          |
| block.number (uint256)                           | 当前区块的 number                                            |
| block.timestamp (uint256)                        | 当前区块的时间戳，为 unix 纪元以来的秒                       |
| block.coinbase (address payable)                 | 当前区块矿工的地址                                           |
| msg.sender (address)                             | 消息发送者 (当前 caller)                                     |
| msg.value (uint256)                              | 当前消息的 wei 值                                            |
| msg.data (bytes calldata)                        | 完整的 calldata                                              |
| msg.sig (bytes4)                                 | calldata 的前四个字节 – (function identifier/即函数标识符)   |
| tx.gasprice (uint256)                            | 交易的 gas 价格                                              |
| tx.origin (address)                              | 交易的发送方 （完整的调用链）                                |
| blockhash(uint256 blockNumber) returns (bytes32) | 给定区块的哈希值 – 只适用于 256 最近区块, 不包含当前区块     |
| gasleft() returns (uint256)                      | 剩余 gas                                                     |
| blockhash() 获取给定区块的哈希值                 | 给定区块的哈希值;在 0.5.0 版本中删除,现使用`blockhash()`获取 |

