---
title: 8-作弊码总结

date: 2023-02-26	

categories: Foundry	

tags: [区块链,区块链知识点总结,合约框架,Foundry]
---	

# Environment  环境

1. **warp**(uint256)：设置block.timestamp

2. **roll**(uint256)：设置block.number

3. **fee**(uint256)：设置block.basefee

4. **difficult**(uint256)：设置block.difficulty

5. **chainId**(uint256)：设置block.chainId

6. **store**(address account, bytes32 slot, bytes32 value)：将 value 值存储在账户 account 的存储槽 slot 中

7. **load**(address account, bytes32 slot) returns (bytes32)：从帐户上的存储槽位加载值

8. **etch**(address who, bytes calldata code)：设置要编码的地址的字节码。

   ```solidity
   bytes memory code = address(awesomeContract).code;
   address targetAddr = address(1);
   vm.etch(targetAddr, code);
   log_bytes(address(targetAddr).code); // 0x6080604052348015610010...
   ```

9. **deal**(address who, uint256 newBalance)：将地址余额设置为`newBalance`

10. **prank**(address)：将msg.sender设置为下次调用的指定地址,“下一个call”也包括静态调用，但不包括对作弊码地址的调用。
    **prank**(address sender, address origin)：如果使用了prank的替代签名，那么在下一次调用时也要设置tx.origin。

11. **startPrank**：参数与prank一样，对于所有后续调用有效，直到 stopPrank 出现

12. **stopPrank**：停止startPrank

13. **record**：告诉VM开始记录所有存储读取和写入，要访问读取和写入，请使用access。

14. **accesses**：获取某个地址上已被读取或写入的所有存储槽，需先调用record()

    ```solidity
    /// contract NumsContract {
    ///     uint256 public num1 = 100; // slot 0
    ///     uint256 public num2 = 200; // slot 1
    /// }
    
    vm.record();
    numsContract.num2();
    (bytes32[] memory reads, bytes32[] memory writes) = vm.accesses(
      address(numsContract)
    );
    emit log_uint(uint256(reads[0])); // 1
    ```

15. **recoedLogs**：通知VM开始记录所有发出的事件，要访问它们，请使用getRecordedLogs。

16. **getRecordLogs**：获取recordLogs记录的已发出事件，这个函数在被调用时将消耗记录的日志。

    ```solidity
    /// event LogCompleted(
    ///   uint256 indexed topic1,
    ///   bytes data
    /// );
    
    vm.recordLogs();
    
    emit LogCompleted(10, "operation completed");
    
    Vm.Log[] memory entries = vm.getRecordedLogs();
    
    assertEq(entries.length, 1);
    assertEq(entries[0].topics[0], keccak256("LogCompleted(uint256,bytes)"));
    assertEq(entries[0].topics[1], bytes32(uint256(10)));
    assertEq(abi.decode(entries[0].data, (string)), "operation completed");
    ```

17. **setNonce**(address account, uint64 nonce)：设置给定Account的nonce，新的nonce必须大于帐户的当前nonce。

18. **getNonce**(address account) returns (uint64)：获取给定Account的nonce

19. **mockCall**(address where, bytes calldata data, bytes calldata retdata)：模拟对一个地址的所有调用，如果调用数据严格或松散地匹配数据，则返回retdata。
    当一个呼叫被发出时，首先检查呼叫数据是否与数据完全匹配。如果不是，则检查调用数据以查看是否有部分匹配，匹配从调用数据的第一个字节开始。
    如果找到匹配，则调用返回retdata。

    mockCall(address where,uint256 value,bytes calldata data,bytes calldata retdata)：使用第二个签名，我们可以使用特定的msg.value来模拟调用。如果有歧义，则Calldata match优先于msg.value。

20. **clearMockedCalls**：清除所有模拟call。

21. **coinbase**(address)：设置block.coinbase

22. **broadcast**( )、**broadcast**(address who)、**broadcast**(uint256 privateKey)：使用调用测试合约的地址或作为发送者提供的地址/私钥，让下一个调用（仅在此调用深度）创建一个稍后可以在链上签名和发送的交易。

23. **startBroadcast**：参数与broadcast一样，使用调用测试合约的地址或作为发送者提供的地址/私钥，让所有后续调用（仅在此调用深度）创建稍后可以在链上签名和发送的交易。

24. **stopBroadcast**：停止收集交易以供以后的链上广播。

25. **pauseGasMetering**：暂停气体计量（即`gasleft()`不随着操作的执行而减少），通过关闭不必要的代码以及长时间运行的脚本（否则会耗尽 Gas）的 Gas 计量，这对于更好地了解 Gas 成本非常有用。

26. **resumeGasMetering**：恢复燃气计量（即`gasleft()`随着操作的执行而减少）。天然气使用量将恢复到暂停时的相同量。

# Assertions 断言

1. **expectRevert**( )、**expectRevert**(bytes4 msg)、**expectRevert**(bytes calldata msg)：如果下一个调用没有返回预期的消息msg，那么expectRevert将返回，在调用expectRevert之后，在恢复调用之前对其他作弊码的调用将被忽略，有3个签名：

   - **不带参数**：断言下一次call reverts，无论消息如何。
   - **With`bytes4`**：断言下一次call reverts为指定的 bytes4。
   - **With`bytes`**：断言下一次call reverts为指定的bytes。

2. **expectEmit**(bool checkTopic1,bool checkTopic2,bool checkTopic3,bool checkData)：断言主题匹配而不检查发射器地址

   **expectEmit**(bool checkTopic1,bool checkTopic2,bool checkTopic3,bool checkData,address emitter)：断言主题匹配并且发出地址匹配

   ```solidity
   event Transfer(address indexed from, address indexed to, uint256 amount);
   
   function testERC20EmitsTransfer() public {
       vm.expectEmit(true, true, false, true, address(myToken));
       emit MyToken.Transfer(address(this), address(1), 10);
       // 执行调用
       myToken.transfer(address(1), 10);
   }
   ```

3. **expectCall**(address where, bytes calldata data)、**expectCall**(address where, uint256 value,bytes calldata data)：期望至少有一个对指定地址的调用，其中调用数据严格或松散匹配数据，第二个还可以检查调用是否使用预期的msg.value进行。

   ```solidity
   Contract target = new Contract();
   vm.expectCall(address(target),1,abi.encodeWithSelector(target.pay.selector, 2));
   target.pay{value: 1}(2);
   ```

# Fuzzer 模糊

1. **assume**(bool)：false，模糊器将丢弃当前的模糊输入并开始新的模糊运行

   ```solidity
   function testSomething(uint256 a) public {
       vm.assume(a != 1);
       require(a != 1);
       // [PASS]
   }
   ```

# Forking 分叉

1. **createFork**(string calldata urlOrAlias)、**createFork**(string calldata urlOrAlias，uint256 block)、**createFork**(string calldata urlOrAlias，bytes32 transaction) returns (uint256)：从给定端点创建新的分叉，并返回该分叉的标识符，如果将块号作为参数传递，则分叉将从该块开始，否则它将从最新的块开始  *latest* ，如果提供了交易哈希，它将把分叉滚动到交易被挖掘的区块，并重播所有以前执行的交易。 

2. **selectFork**(uint256 forkId)：接受由createFork创建的fork标识符，并将相应的fork状态设置为活动状态。

   ```solidity
   uint256 forkId = vm.createFork(MAINNET_RPC_URL, 1_337_000);
   vm.selectFork(forkId);
   
   assertEq(block.number, 1_337_000);
   ```

3. **createSelectFork**(string calldata urlOrAlias)、**createSelectFork**(string calldata urlOrAlias，uint256 block) returns (uint256)：
   createFork + selectFork

4. **activeFork**() returns (uint256)：返回当前活动分叉的标识符，如果当前没有分叉处于活动状态，则还原。 

5. **rollFork**(uint256 blockNumber)、**rollFork**(bytes32 transaction)、**rollFork**(uint256 forkId, uint256 blockNumber)、**rollFork**(uint256 forkId, bytes32 transaction)：更新分叉，如果没传forkId表示更新当前活动分叉

6. **makePersistent**(address)、**makePersistent**(address，address)、**makePersistent**(address，address，address)、**makePersistent**(address[] calldata)：将指定帐户标记为持久帐户，每个叉子createFork有自己独立的存储，只有测试合约账户和调用者在分叉之间是持久的

7. **revokePersistent**(address)、**revokePersistent**(address[] calldata)：将指定帐户撤销标记为持久帐户

8. **isPersistent**(address) returns (bool)：判断指定帐户标记是否为持久帐户

9. **allowCheatcodes**(address)：在分叉模式下，显式授予给定地址作弊码访问权限

10. **transact**(bytes32 txHash)、**transact**(uint256 forkId, bytes32 txHash)：在分叉模式下，获取交易并在当前状态下执行交易

# External 外部

1. **ffi**(string[] calldata) returns (bytes memory)：如果启用了ffi，则可以调用任意命令

   ```solidity
   string[] memory inputs = new string[](3);
   inputs[0] = "echo";
   inputs[1] = "-n";
   inputs[2] = "gm";
   
   bytes memory res = vm.ffi(inputs);
   assertEq(string(res), "gm");
   ```

2. **projectRoot**() returns (string memory)：返回当前项目的根目录

3. **getCode**(string calldata) returns (bytes memory)：返回给定合约路径的创建字节码。调用数据参数可以是以下形式  `ContractFile.sol` （如果文件名和合约名称相同），  `ContractFile.sol:ContractName`或相对于项目根目录的项目路径。 

   ```solidity
   MyContract myContract = new MyContract(arg1, arg2);
   bytes memory args = abi.encode(arg1, arg2);
   bytes memory bytecode = abi.encodePacked(vm.getCode("MyContract.sol:MyContract"), args);
   address anotherAddress;
   assembly {
       anotherAddress := create(0, add(bytecode, 0x20), mload(bytecode))
   }
   assertEq0(address(myContract).code, anotherAddress.code); // [PASS]
   ```

4. **getDeployedCode**(string calldata) returns (bytes memory)：只返回部署的字节码

   ```solidity
   contract Override {
       event Payload(address sender, address target, bytes data);
       function emitPayload(address target, bytes calldata message) external payable returns (uint256) {
           emit Payload(msg.sender, target, message);
           return 0;
       }
   }
   
   bytes memory code = vm.getDeployedCode("Override.sol:Override");
   address overrideAddress = address(64);
   vm.etch(overrideAddress, code);
   assertEq(overrideAddress.code,code)
   ```

5. **setEnv**(string calldata key, string calldata value)：设置环境变量 `key=value`

6. **envOr**：读取任何类型的环境变量的无失败方法：如果请求的环境键不存在，`envOr()`将返回默认值而不是revert（也适用于数组）,
   返回的类型由传递的参数`defaultValue`类型决定，delimiter为分割符

   ```solidity
   function envOr(string calldata key, bool defaultValue) external returns (bool value);
   function envOr(string calldata key, uint256 defaultValue) external returns (uint256 value);
   function envOr(string calldata key, int256 defaultValue) external returns (int256 value);
   function envOr(string calldata key, address defaultValue) external returns (address value);
   function envOr(string calldata key, bytes32 defaultValue) external returns (bytes32 value);
   function envOr(string calldata key, string calldata defaultValue) external returns (string memory value);
   function envOr(string calldata key, bytes calldata defaultValue) external returns (bytes memory value);
   
   function envOr(string calldata key, string calldata delimiter, bool[] calldata defaultValue) external returns (bool[] memory value);
   function envOr(string calldata key, string calldata delimiter, uint256[] calldata defaultValue) external returns (uint256[] memory value);
   function envOr(string calldata key, string calldata delimiter, int256[] calldata defaultValue) external returns (int256[] memory value);
   function envOr(string calldata key, string calldata delimiter, address[] calldata defaultValue) external returns (address[] memory value);
   function envOr(string calldata key, string calldata delimiter, bytes32[] calldata defaultValue) external returns (bytes32[] memory value);
   function envOr(string calldata key, string calldata delimiter, string[] calldata defaultValue) external returns (string[] memory value);
   function envOr(string calldata key, string calldata delimiter, bytes[] calldata defaultValue) external returns (bytes[] memory value);
   ```

7. **envBool**(string calldata key) returns (bool value)、**envBool**(string calldata key, string calldata delimiter) returns (bool[] memory values);：将环境变量读取为 `bool`或者 `bool[]`

8. **envUint**：将环境变量读取为 `uint256`或者 `uint256[]`. 

9. **envInt**：将环境变量读取为 `int256`或者 `int256[]`

10. **envAddress**：将环境变量读取为 `address`或者 `address[]`

11. **envBytes32**：将环境变量读取为 `bytes32`或者 `address[]`. 

12. **envString**：将环境变量读取为 `string`或者 `string[]`

13. **envBytes**：将环境变量读取为 `bytes`或者 `bytes[]`. 

14. **parseJson**：解析字符串形式的 JSON 文件

    ```solidity
    string memory root = vm.projectRoot();
    string memory path = string.concat(root, "/src/test/fixtures/broadcast.log.json");
    string memory json = vm.readFile(path);
    bytes memory transactionDetails = json.parseRaw(".transactions[0].tx");
    RawTx1559Detail memory rawTxDetail = abi.decode(transactionDetails, (RawTx1559Detail));
    
    // 返回与“key”对应的值
    vm.parseJson(string memory json, string memory key)
    // 返回整个json文件
    vm.parseJson(string memory json);
    ```

# Utilities 实用程序

1. **addr**(uint256 privateKey) returns (address)：计算给定私钥的地址。

2. **sign**(uint256 privateKey, bytes32 digest) returns (uint8 v, bytes32 r, bytes32 s)：使用私钥 privateKey 签署摘要摘要，返回 (v, r, s)。 这对于测试获取签名数据并执行 ecrecover 来验证签名者的函数非常有用。

   ```solidity
   address alice = vm.addr(1);
   bytes32 hash = keccak256("Signed by Alice");
   (uint8 v, bytes32 r, bytes32 s) = vm.sign(1, hash);
   address signer = ecrecover(hash, v, r, s);
   assertEq(alice, signer); // [PASS]
   ```

3. **label**(address addr, string calldata label)：为测试跟踪中的 addr 设置标签 label， 如果地址被标记，则测试跟踪中将显示标签而不是地址。

4. **deriveKey**(string calldata mnemonic, uint32 index) returns (uint256)：在导出路径m/44'/60'/0'/0/{index}处导出。

   **deriveKey**(string calldata mnemonic, string calldata path,uint32 index) returns (uint256)：允许您指定派生路径作为第二个参数。

   从给定的助记词或助记词文件路径派生私钥。 

5. **rememberKey**(uint256 privateKey) returns (address)：将私钥存储在forge的本地钱包中并返回相应的地址，该地址稍后可用于广播。

6. **toString**：将任何类型转换为其字符串版本。

# Snapshots 快照

1.**snapshot**() returns(uint256)：拍摄区块链状态的快照并返回创建的快照的标识符

2.**revertTo**(uint256) returns(bool)：将区块链的状态恢复到给定的快照。 这将删除给定的快照以及此后拍摄的任何快照（例如：恢复到 id 1 将删除 id 2、3 等的快照）

```solidity
struct Storage {
    uint slot0;
    uint slot1;
}

contract SnapshotTest is Test {
    Cheats constant cheats = Cheats(HEVM_ADDRESS);

    Storage store;

    function setUp() public {
        store.slot0 = 10;
        store.slot1 = 20;
    }

    function testSnapshot() public {
        uint256 snapshot = cheats.snapshot();
        store.slot0 = 300;
        store.slot1 = 400;

        assertEq(store.slot0, 300);
        assertEq(store.slot1, 400);

        // after resetting to a snapshot all changes are discarded
        cheats.revertTo(snapshot);
        assertEq(store.slot0, 10, "snapshot revert for slot 0 unsuccessful");
        assertEq(store.slot1, 20, "snapshot revert for slot 1 unsuccessful");
    }

}
```

# RPC 远程过程调用

**rpcUrl**(string calldata alias) returns(string memory)：返回已配置别名的 URL

**rpcUrls**() returns(string[2] [] memory)：返回所有配置的（别名、URL）对

```toml
# --snip--
[rpc_endpoints]
optimism = "https://optimism.alchemyapi.io/v2/..."
mainnet = "${RPC_MAINNET}" 

string memory url = vm.rpcUrl("optimism");
assertEq(url, "https://optimism.alchemyapi.io/v2/...");
```

# Files 文件

```solidity
// 读取文件的全部内容到字符串，(path) => (data)
function readFile(string calldata) external returns (string memory);
// 读取文件的下一行为字符串，(path) => (line)
function readLine(string calldata) external returns (string memory);
// 将数据写入文件，如果不存在则创建一个文件，如果存在则完全替换其内容,(path, data) => ()
function writeFile(string calldata, string calldata) external;
// 将行写入文件，如果不存在则创建一个文件,(path, data) => ()
function writeLine(string calldata, string calldata) external;
// 关闭文件读取，重置偏移量并允许从readLine开始读取,(path) => ()
function closeFile(string calldata) external;
// 删除文件。该作弊代码将在以下情况下恢复，但不限于这些情况:Path指向一个目录、文件不存在、用户没有权限删除文件,(path) => ()
function removeFile(string calldata) external;
```



